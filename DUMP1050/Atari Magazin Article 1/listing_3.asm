; Listing 3 
; LAEDT DURCH DEN X-BEFEHL DAS 
; FLOPPY BETRIEB8SYSTEM IN DEN 
; COMPUTER  
; ( 1. 4K NACH PUF1 ) 
; ( 2. 4K NACH PUF2 )
;
DSBI	= 	$0300 
DDRV	=	$0301 
DKMD	=	$0302 
DSTA	=	$0303 
DPUF	=	$0304 
DTIO	=	$0306 
DLEN	=	$0308   
DAUX  	=	$030A 
SIO		=	$E459 
;
PUF1 	=	$6000
PUF2 	=	$7000 

		* = $5000 
		LDA #<PUF1 		; 1. 4K ROM 
		STA DPUF 		; NACH PUF1
		LDA	#>PUF1 
		STA DPUF+1 
		LDA # <$F000 	; NEG. DAUX+1 
		STA DAUX		; SPRICHT 
		LDA # >$F000 	; 1.4K ROM 
		STA DAUX+1		; AN  
;		 
LOOP1 	JSR DOSIO		; LADE 1. 4K ROM 
		INC DPUF+1		; IN COMPUTER 
		INC DAUX+1 
		LDA DAUX+1 
		BNE LOOP1
; 
		LDA # <PUF2		; 2. 4K ROM 
		STA DPUF 		; NACH PUF2
		LDA # >PUF2 
		STA DPUF+1 
		LDA # <$7000 	; POS. DAUX+1 
		STA DAUX 		; SPRICHT
		LDA # >$7000 	; 2. 4K ROM 
		STA DAUX+1 		; AN
;
LOOP2	JSR DOSIO 		; LADE 2. 4K ROM 
		INC DAUX+1		; IN COMPUTER 
		INC DPUF+1 
		LDA DAUX+1 
		CMP #$80 
		BNE LOOP2
		
;
; DAS FLOPPY-ROM BEFINDET SICH 
; JETZT IM COMPUTER SPEICHER 
; 1. 4K BEI PUF1 
; 2. 4K BEI PUF2 
; 
		RTS 
;
DOSIO	LDA #'1 
		STA DSBI 
		LDA #1 
		STA DDRV 
		LDA #'X 		; DAS NEUE
		STA DKMD 		; KOMMANDO
		LDA #$40 		; STATUS READ
		STA DSTA 
		LDA #2 
		STA DTIO 
		LDA #0 
		STA DTIO+1 		; 256 BYTES
		STA DLEN 		; WERDEN VON
		LDA #1 			; DER FLOPPY
		STA DLEN+1 		; ERWARTET
		JSR SIO 
		BMI FEHLER 
		RTS 
;
FEHLER		
		LDA 710 		; BEI FEHLER 
		PHA 			; HINTERGRUND-
		SBC #8			; IFARBE AENDERN
		STA 710 
		LDA #0 
		STA 20 
WARTE	LDA 20 			; WARTEN
		CMP #50 
		BCC WARTE 
		PLA 			; ALTE HINTER-
		STA 710 		; GRUNDFARBE
		PLA 
		PLA 
		RTS 
		
;
; STARTADRESSE 
;
	* = $02E0 
	.WORD $5000 
		
