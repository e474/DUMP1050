;Listing 1 

; PROGRAMMIERT DIE FLOPPY MIT 
; DEM NEUEN X-BEFEHL, DER IM 
; SPEICHER BEI BEFADR STEHEN MUSS 
;
DSBI 	= 	$0300 
DDRV 	= 	$0301 
DKMD 	= 	$0302 
DSTA 	= 	$0303 
DPUF 	= 	$0304 
DTIO 	= 	$0306
DLEN 	= 	$0308 
DAUX 	= 	$030A 
SIO  	= 	$E459 
;
PUF 	= 	$0600 
BEFTAB	=	$9780 
BEFADR	=	$9000
;

		* =  $A000 
;
;		BEFEHLSTABELLE LADEN 
;
		LDX #<PUF 
		LDY #>PUF 
		JSR SETPUF 
		LDX #<BEFTAB 
		LDY #>BEFTAB 
		JSR SETAUX 
		LDA #'R 
		JSR DOSIO

;
; LEEREN BEFEHLSPLATZ SUCHEN 
;
		LDY #$20 
SUCH	LDA PUF,Y 
		BEQ LEER 
		INY 
		JMP SUCH 
;
;		NEUEN BEFEHL EINFUEGEN 
;
LEER 	LDA #'X 				; NEUER BEFEHL  
		STA PUF, Y 				; X
		LDA #<BEFADR 
		STA PUF+$20,Y			; BEFEHLSADR. 
		LDA #>BEFADR 			; SETZEN 
		STA PUF+$40,Y 
		
; NEUE TABELLE ZUR FLOPPY 

		LDA #'P 
		JSR DOSIO 
		
;	
; NEUEN BEFEHL ZUR FLOPPY 
; SCHICKEN 
; ( COMPUTER-RAM $9000-$93FF )
; ( IN FLOPPY-RAM $9000-$93FF ) 
;

		LDX # <BEFADR 
		LDY # >BEFADR 
		JSR SETPUF 
		JSR SETAUX 
PROG	LDA #'P 
		JSR DOSIO 
		CLC 
		LDA DPUF 
		ADC #$80 
		STA DPUF 
		BCC *+5 
		INC DPUF+1 
		LDA DPUF 
		STA DAUX 
		LDA DPUF+1 
		STA DAUX+1 		; KOMMAND0 KANN
		CMP #$94
		BCC PROG 		; GEHEN 
;
;RAMBEREICH IN FLOPPY SCHUETZEN
;
		LDX #$60
		LDY #$60
		JSR SETAUX 
		LDA #'H 
		JSR DOSIO 
;
; FERTIG, FLOPPY PROGRAMIERT 
;
		RTS

;
SETPUF 	STX DPUF 
		STY DPUF+1 
		RTS 
SETAUX		
		STX DAUX 
		STY DAUX+1 
		RTS 
DOSIO 	LDX #0
		CMP #'P 
		BNE *+4 
		LDX #$80
		CMP #'R 
		BNE *+4 
		LDX #$40 
		STX DSTA 
		STA DKMD 
		LDA #'1
		
		STA DSBI 
		LDA #1 
		STA DDRV 
		LDA #2 
		STA DTIO 
		LDA #128 
		STA DLEN
		LDA #0 
		STA DTIO+1 
		STA DLEN+1 
		JSR SIO
		BMI FEHLER 
		RTS 
;
FEHLER		
		LDA 710 		; BEI FEHLER 
		PHA 			; HINTERGRUND-
		SBC #8			; IFARBE AENDERN
		STA 710 
		LDA #0 
		STA 20 
WARTE	LDA 20 			; WARTEN
		CMP #50 
		BCC WARTE 
		PLA 			; ALTE HINTER-
		STA 710 		; GRUNDFARBE
		PLA 
		PLA 
		RTS 
		
;
; STARTADRESSE 
;
	* = $02E0 
	.WORD $A000 
	
